import java.text.SimpleDateFormat

Date buildTimeAndDate = new Date()
ext {
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

def projectArtifactId = 'swagger2markup'

jar {
    manifest {
        attributes(
                'Built-By': 'Robert Winkler',
                'Created-By': System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
                'Build-Date': project.buildDate,
                'Build-Time': project.buildTime,
                'Specification-Title': projectArtifactId,
                'Specification-Version': project.version,
                'Implementation-Title': projectArtifactId,
                'Implementation-Version': project.version
        )
    }
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''
if (!project.hasProperty('gpgPassphrase')) ext.gpgPassphrase = ''
if (!project.hasProperty('ossUser')) ext.ossUser = ''
if (!project.hasProperty('ossPassword')) ext.ossPassword = ''

bintray {
    user = project.bintrayUsername
    key = project.bintrayApiKey
    dryRun = true //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'swagger2markup'
        websiteUrl = 'https://github.com/RobWin/swagger2markup'
        issueTrackerUrl = 'https://github.com/RobWin/swagger2markup/issues'
        vcsUrl = 'https://github.com/RobWin/swagger2markup.git'
        desc = 'A Swagger to Markup (AsciiDoc and Markdown) converter.'
        licenses = ['Apache-2.0']
        version {
            vcsTag = project.version
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = project.gpgPassphrase //Optional. The passphrase for GPG signing'
            }
            mavenCentralSync {
                sync = true //Optional (true by default). Determines whether to sync the version to Maven Central.
                user = ossUser //OSS user token
                password = ossPassword //OSS user password
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom.withXml {
                def devs = ['RobWin': 'Robert Winkler']
                def root = asNode()
                root.appendNode('name', 'swagger2markup')
                root.appendNode('packaging', 'jar')
                root.appendNode('url', 'https://github.com/RobWin/swagger2markup')
                root.appendNode('description', 'A Swagger to Markup (AsciiDoc and Markdown) converter.')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'Apache-2.0')
                license.appendNode('url', 'https://github.com/RobWin/swagger2markup/blob/master/LICENSE.txt')
                license.appendNode('distribution', 'repo')

                root.appendNode('scm').appendNode('url', 'https://github.com/RobWin/swagger2markup.git')

                def developers = root.appendNode('developers')
                devs.each {
                    def d = developers.appendNode('developer')
                    d.appendNode('id', it.key)
                    d.appendNode('name', it.value)
                }
            }
            artifact sourcesJar
            artifact javadocJar
        }
    }
}